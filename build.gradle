/*
 *  Copyright 2017 Regents of the University of California.
 *
 *  Licensed under the Educational Community License, Version 2.0 (the "license");
 *  you may not use this file except in compliance with the License. You may
 *  obtain a copy of the license at
 *
 *  https://opensource.org/licenses/ECL-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage
import org.apache.tools.ant.BuildException
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'io.github.robwin:jgitflow-gradle-plugin:0.5.0'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'pmd'
    id 'findbugs'
    id 'jacoco'
    id 'maven-publish'
    id 'org.springframework.boot'               version '1.5.2.RELEASE'
    id 'com.jfrog.artifactory'                  version '4.4.14'
    id 'com.bmuschko.docker-remote-api'         version '3.0.11'
    id "org.flywaydb.flyway"                    version "4.2.0"

}

apply plugin: 'io.github.robwin.jgitflow'
apply plugin: 'project-report'

/***************************
 * dependency management
 ***************************/

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://airdev.jfrog.io/airdev/libs-releases/" }
    maven { url "https://airdev.jfrog.io/airdev/libs-snapshots/" }
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

dependencies {
    compile "org.opentestsystem.ap:ap-common:${commonVersion}"

    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-aop'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-log4j2'

    compile 'org.springframework.cloud:spring-cloud-starter-config'
    compile 'org.springframework.cloud:spring-cloud-starter-zuul'

    compile 'org.springframework.security.oauth:spring-security-oauth2:2.3.0.RELEASE'
    
    compile 'org.springframework.retry:spring-retry'

    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'org.apache.commons:commons-lang3:3.5'

    compile 'com.google.guava:guava:22.0'

    compile 'org.apache.logging.log4j:log4j-api:2.10.0'
    compile 'org.apache.logging.log4j:log4j-core:2.10.0'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.10.0'

    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    compile 'com.lmax:disruptor:3.3.7'

    compile 'com.okta.jwt:okta-jwt-verifier:0.4.0'
    compile 'com.okta.jwt:okta-jwt-verifier-impl:0.4.0'

    compileOnly 'org.projectlombok:lombok'

    runtime 'org.springframework.boot:spring-boot-devtools'

    testCompile 'org.springframework.boot:spring-boot-starter-test'

    testCompile 'org.projectlombok:lombok'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Dalston.RELEASE'
    }
}

/***************************
 * build information
 ***************************/

springBoot {
    buildInfo()
}

processResources {
    filesMatching("**/application.yml") {
        expand project.properties
    }
}

/***************************
 * publishing
 ***************************/

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing.publications {
    mavenJava(MavenPublication) {
        from components.java

        artifact sourceJar {
            classifier "sources"
        }
    }
}

artifactory {
    contextUrl = artifactoryUrl

    publish {
        repository {
            repoKey  = project.version.contains('-SNAPSHOT') ? project.artifactorySnapshotPublish : project.artifactoryReleasePublish
            username = project.artifactoryUser
            password = project.artifactoryPassword
            maven	 = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

/***************************
 * kubernetes (k8s)
 ***************************/

task k8sCopyFiles(type: Copy) {

    from('src/main/kubernetes/') {
        filter(ReplaceTokens, tokens: [version              : project.version,
                                       dockerTagBase        : project.dockerTagBase,
                                       kubecfgState         : project.kubecfgState,
                                       kubecfgName          : project.kubecfgName,
                                       kubecfgDeployment    : project.kubecfgDeployment])
    }

    into 'build/kubernetes'
}

task k8sCopyFilesUat(type: Copy) {

    from('src/main/kubernetes/') {
        filter(ReplaceTokens, tokens: [version              : project.version,
                                       dockerTagBase        : project.dockerTagBase,
                                       kubecfgState         : project.kubecfgState,
                                       kubecfgName          : project.kubecfgNameUat,
                                       kubecfgDeployment    : project.kubecfgDeployment])
    }

    into 'build/kubernetes'
}

/***************************
 * docker
 ***************************/

docker {
    registryCredentials {
        username = project.dockerHubUser
        password = project.dockerHubPassword
        email = project.dockerHubEmail
    }
}

task dockerCopyFiles(type: Copy) {
    dependsOn 'bootRepackage'

    from('src/main/docker/') {
        filter(ReplaceTokens, tokens: [USER_HOME:           System.env.HOME,
                                       configServerVersion: project.configServerVersion,
                                       version:             project.version,
                                       dockerTagBase:       project.dockerTagBase])
    }

    from("build/libs/${jar.archiveName}") {
        // drop version from artifact name
        rename { String fileName ->
            fileName.replace("-${project.version}", "")
        }
    }

    into 'build/docker'
}

task dockerBuildImage(type: DockerBuildImage) {
    dependsOn 'dockerCopyFiles'
    inputDir = project.file('build/docker/')
    def name = "${dockerTagBase}/${jar.baseName}" + (project.version.contains('-SNAPSHOT') ? "": ":${project.version}")
    tags.add(name.toString())
}

task dockerPushImage(type: DockerPushImage) {
    dependsOn 'dockerBuildImage'
    def name = "${dockerTagBase}/${jar.baseName}" + (project.version.contains('-SNAPSHOT') ? "": ":${project.version}")
    imageName = name.toString()
}

task dockerRemoveImage(type: DockerRemoveImage) {
    def name = "${dockerTagBase}/${jar.baseName}" + (project.version.contains('-SNAPSHOT') ? "": ":${project.version}")
    imageId = name.toString()
}

/***************************
 * Checkstyle
 ***************************/

checkstyle {
    ignoreFailures = true
}

/***************************
 * PMD
 ***************************/

pmd {
    ignoreFailures = true

    ruleSets = [
            'java-basic',
            'java-braces',
            'java-clone',
            'java-codesize',
            'java-coupling',
            'java-design',
            'java-empty',
            'java-finalizers',
            'java-imports',
            'java-j2ee',
            'java-javabeans',
            'java-junit',
            'java-naming',
            'java-optimizations',
            'java-strictexception',
            'java-strings',
            'java-sunsecure',
            'java-typeresolution',
            'java-unnecessary',
            'java-unusedcode'
    ]
}

/***************************
 * FindBugs
 ***************************/

findbugs {
    ignoreFailures = true
    reportLevel = "medium"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
	    html.stylesheet resources.text.fromFile('config/findbugs/findbugs-color.xsl')
    }
}

/***************************
 * set common version
 ***************************/

task updateCommonVersion {
    doLast {
        doCommonVersionUpdate(project.newCommonVersion)
    }
}

def doCommonVersionUpdate(String version) {
    String currentVersion = project.commonVersion
    File propertiesFile = project.file(Project.GRADLE_PROPERTIES)
    if (!propertiesFile.file) {
        propertiesFile.append("commonVersion=${version}")
    }else {
        try {
            project.ant.replace(file: propertiesFile, token: "commonVersion=${currentVersion}", value: "commonVersion=${version}", failOnNoReplacements: true)
        } catch (BuildException e) {
            throw new GradleException("Failed to update version in ${Project.GRADLE_PROPERTIES}.  Check the property exists and is formatted correctly.")
        }
    }
}

/***************************
 * gradle wrapper
 ***************************/

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

/***************************
 * Lifecycle enhancements
 ***************************/

tasks.artifactoryPublish.dependsOn 'build'

test.finalizedBy(jacocoTestReport)
